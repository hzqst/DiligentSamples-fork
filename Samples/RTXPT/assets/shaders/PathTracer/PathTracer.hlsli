#ifndef __PATH_TRACER_HLSLI__
#define __PATH_TRACER_HLSLI__

#include "Config.h"
#include "PathTracerBridge.hlsli"
#include "PathTracerHelpers.hlsli"
#include "Lighting/EnvMap.hlsli"
#include "Lighting/LightSampler.hlsli"
#include "Rendering/Materials/BxDF.hlsli"
#include "Utils/StatelessSampleGenerators.hlsli"
#include "Rendering/Volumes/HomogeneousVolumeSampler.hlsli"
#include "PathTracerNestedDielectrics.hlsli"

#include "PathState.hlsli"
#include "PathPayload.hlsli"

#if PATH_TRACER_MODE != PATH_TRACER_MODE_REFERENCE || defined(__INTELLISENSE__)
#    include "PathTracerStablePlanes.hlsli"
#endif

static const float kVisibilityRayTMin = 0.0;
static const float kVisibilityRayTMax = 1e30;

namespace PathTracer
{
    static const float kSpecularRoughnessThreshold = 0.25;

    inline PathState EmptyPathInitialize(uint2 pixelPos, float pixelConeSpreadAngle);

    inline PathPayload MakeVisibilityPayload(uint2 pixelPos)
    {
        PathState visibilityPath = EmptyPathInitialize(pixelPos, 0.0);
        visibilityPath.setActive();
        visibilityPath.clearHit();
#if PATH_TRACER_MODE != PATH_TRACER_MODE_BUILD_STABLE_PLANES
        visibilityPath.SetL(float4(0.0, 0.0, 0.0, 0.0));
#endif
        visibilityPath.SetThp(float3(0.0, 0.0, 0.0));
        return PathPayload::pack(visibilityPath);
    }

    bool TraceVisibilityRay(float3 origin, float3 dir, float tMax)
    {
        if (tMax <= kVisibilityRayTMin)
            return false;

        RayDesc ray;
        ray.Origin    = origin;
        ray.Direction = dir;
        ray.TMin      = kVisibilityRayTMin;
        ray.TMax      = tMax;

        PathPayload payload = MakeVisibilityPayload(Bridge::getPixelPosition());
        TraceRay(t_SceneBVH,
                 RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH | RAY_FLAG_SKIP_CLOSEST_HIT_SHADER,
                 0xFF,
                 RTXPT_VISIBILITY_RAY_INDEX,
                 RTXPT_HIT_GROUP_STRIDE,
                 RTXPT_VISIBILITY_RAY_INDEX,
                 ray,
                 payload);
        return !PathPayload::unpack(payload).isActive();
    }

    float3 MakeVisibilityOrigin(float3 hitPos, float3 faceNormal, float3 shadingNormal, float3 dir)
    {
        const float side = dot(shadingNormal, dir) >= 0.0 ? 1.0 : -1.0;
        return ComputeRayOrigin(hitPos, faceNormal * side);
    }

    float ComputeShadowNoLFadeout(float3 lightDir, float3 vertexNormal, float shadowNoLFadeout)
    {
        return shadowNoLFadeout > 0.0 ?
            ComputeLowGrazingAngleFalloff(lightDir, vertexNormal, shadowNoLFadeout, 2.0 * shadowNoLFadeout) :
            1.0;
    }

#if PATH_TRACER_MODE != PATH_TRACER_MODE_BUILD_STABLE_PLANES
    float ComputeLightVsBSDFMISForLightSample(DirectLightSample sample, uint fullSamples)
    {
        if (!sample.sampleableByBSDF || sample.bsdfPdf <= 0.0 || sample.proposalPdf <= 0.0)
            return 1.0;

        const float lightPdf = sample.proposalPdf * float(max(fullSamples, 1u));
        return PowerHeuristic(1.0, lightPdf, 1.0, sample.bsdfPdf);
    }

    float3 SampleEnvironmentNEE(StandardBSDFData bsdfData, float3 hitPos, float3 faceNormal, float3 vertexNormal,
                                float shadowNoLFadeout, float3 wo, inout SampleGenerator sg, float fireflyFilterK)
    {
        EnvMapSampler envSampler = RTXPTCreateEnvMapSampler(Bridge::getEnvMapConstants());
        const DistantLightSample envSample = envSampler.MIPDescentSample(sampleNext2D(sg));
        if (envSample.Pdf <= 0.0)
            return float3(0.0, 0.0, 0.0);

        const float specProb = getSpecularProbability(bsdfData, wo);
        float3      f;
        float       bsdfPdf;
        EvalBSDF(bsdfData, wo, envSample.Dir, specProb, f, bsdfPdf);
        if (bsdfPdf <= 0.0)
            return float3(0.0, 0.0, 0.0);

        const float3 visibilityOrigin = MakeVisibilityOrigin(hitPos, faceNormal, bsdfData.N, envSample.Dir);
        const bool visible = TraceVisibilityRay(visibilityOrigin, envSample.Dir, kVisibilityRayTMax);
        if (!visible)
            return float3(0.0, 0.0, 0.0);

        const float misWeight = PowerHeuristic(1.0, envSample.Pdf, 1.0, bsdfPdf);
        const float fadeOut   = ComputeShadowNoLFadeout(envSample.Dir, vertexNormal, shadowNoLFadeout);
        float3      contribution = f * envSample.Le * (fadeOut * misWeight / envSample.Pdf);

        const float ffThreshold = g_Const.ptConsts.fireflyFilterThreshold;
        if (ffThreshold != 0.0)
        {
            const float neeK = ComputeNewScatterFireflyFilterK(fireflyFilterK, envSample.Pdf, 1.0);
            contribution *= FireflyFilterShort(Average(contribution), ffThreshold, neeK);
        }

        return contribution;
    }

    float ComputeBSDFMISForEmissiveTriangle(uint2 pixelPos, float bsdfPdf, float emissiveSolidAnglePdf, uint fullSamples)
    {
        if (bsdfPdf <= 0.0 || emissiveSolidAnglePdf <= 0.0)
            return 1.0;

        const float selectionPdf = GetEmissiveTriangleSelectionPdf(pixelPos);
        if (selectionPdf <= 0.0)
            return 1.0;

        const float lightPdf = selectionPdf * emissiveSolidAnglePdf * float(max(fullSamples, 1u));
        return PowerHeuristic(1.0, bsdfPdf, 1.0, lightPdf);
    }

    float3 SampleDirectLightNEE(StandardBSDFData bsdfData, float3 hitPos, float3 faceNormal, float3 vertexNormal,
                                float shadowNoLFadeout, float3 wo, uint2 pixelPos, inout SampleGenerator sg,
                                float fireflyFilterK, out bool sampledEmissive)
    {
        sampledEmissive = false;

        const uint fullSamples = min(32u, g_Const.ptConsts.NEEFullSamples);
        if (fullSamples == 0u || Bridge::getTotalLightCount() == 0u)
            return float3(0.0, 0.0, 0.0);

        const uint candidateSamples = max(1u, min(32u, g_Const.ptConsts.NEECandidateSamples));

        float3 result = float3(0.0, 0.0, 0.0);
        [loop]
        for (uint sampleIndex = 0u; sampleIndex < fullSamples; ++sampleIndex)
        {
            NEEWeightedReservoirSampler wrs = NEEWeightedReservoirSampler::make();

            [loop]
            for (uint candidateIndex = 0u; candidateIndex < candidateSamples; ++candidateIndex)
            {
                DirectLightSample candidate = GenerateDirectLightCandidate(bsdfData, hitPos, wo, pixelPos, sg);
                wrs.Add(sampleNext1D(sg), candidate, EvalDirectLightCandidateWeight(candidate));
            }

            DirectLightSample picked               = wrs.candidate;
            const float       candidateProbability = wrs.CandidateProbability();
            if (!picked.valid || candidateProbability <= 0.0)
                continue;

            const float visibilityDistance =
                picked.kind == kLightProxyKindEmissiveBucket ? picked.distance * 0.9985 : picked.distance;
            const float3 visibilityOrigin = MakeVisibilityOrigin(hitPos, faceNormal, bsdfData.N, picked.dir);
            const bool visible = TraceVisibilityRay(visibilityOrigin, picked.dir, visibilityDistance);
            if (!visible)
                continue;

            const float wrsScale   = 1.0 / (candidateProbability * float(candidateSamples));
            const float misWeight  = ComputeLightVsBSDFMISForLightSample(picked, fullSamples);
            const float fadeOut    = ComputeShadowNoLFadeout(picked.dir, vertexNormal, shadowNoLFadeout);
            float3      contribution = picked.bsdfF * picked.radianceOverPdf * (fadeOut * wrsScale * misWeight / float(fullSamples));

            const float ffThreshold = g_Const.ptConsts.fireflyFilterThreshold;
            if (ffThreshold != 0.0)
            {
                const float neeK = ComputeNewScatterFireflyFilterK(fireflyFilterK, picked.proposalPdf, 1.0);
                contribution *= FireflyFilterShort(Average(contribution), ffThreshold, neeK);
            }

            const uint feedbackLightIndex =
                picked.kind == kLightProxyKindAnalytic ? picked.index : Bridge::getEmissiveBucketLightIndex();
            InsertFeedbackFromNEE(pixelPos, feedbackLightIndex, contribution, sampleNext1D(sg));
            sampledEmissive = sampledEmissive || picked.kind == kLightProxyKindEmissiveBucket;
            result += contribution;
        }

        return result;
    }
#endif

#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
    float ComputeBSDFEnvMISWeight(bool didEnvNEE, float prevBsdfPdf, float3 rayDir)
    {
        if (!didEnvNEE || prevBsdfPdf <= 0.0)
            return 1.0;

        EnvMapSampler envSampler = RTXPTCreateEnvMapSampler(Bridge::getEnvMapConstants());
        const float envPdf = envSampler.MIPDescentEvalPdf(rayDir);
        if (envPdf <= 0.0)
            return 1.0;

        return PowerHeuristic(1.0, prevBsdfPdf, 1.0, envPdf);
    }
#endif

    inline bool ReferenceShouldRunNEE(const PathState preScatterPath, const WorkingContext workingContext)
    {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        const uint maxBounces    = max(workingContext.PtConsts.bounceCount, 1u);
        const uint maxNEEBounces = min(workingContext.PtConsts.maxNEEBounceCount, maxBounces);
        return preScatterPath.getVertexIndex() <= maxNEEBounces;
#else
        return true;
#endif
    }

    inline float3 ApplyReferenceFireflyFilter(float3 radiance, const PathState path, const WorkingContext workingContext)
    {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        const float ffThreshold = workingContext.PtConsts.fireflyFilterThreshold;
        if (ffThreshold != 0.0)
            radiance = FireflyFilter(radiance, ffThreshold, path.GetFireflyFilterK());
#endif
        return radiance;
    }

    inline bool HasFinishedSurfaceBounces(uint vertexIndex, uint diffuseBounces)
    {
        if (g_Const.ptConsts.bounceCount < vertexIndex)
            return true;
        return diffuseBounces > g_Const.ptConsts.diffuseBounceCount;
    }

    inline PathState EmptyPathInitialize(uint2 pixelPos, float pixelConeSpreadAngle)
    {
        PathState path;
        path.SetId(PathIDFromPixel(pixelPos));
        path.flags = 0;
        path.vertexIndex = 0;
        path.SetSceneLength(0.0);
        path.packedCounters = 0;

#if RTXPT_NESTED_DIELECTRICS_QUALITY > 0 || defined(__INTELLISENSE__)
        for (uint i = 0; i < INTERIOR_LIST_SLOT_COUNT; ++i)
            path.interiorList.slots[i] = 0;
#endif

        path.SetOrigin(float3(0.0, 0.0, 0.0));
        path.SetDir(float3(0.0, 0.0, 0.0));
        path.SetThp(float3(1.0, 1.0, 1.0));
        path.setActive();
        path.setDeltaOnlyPath(true);
        path.rayCone = RayCone::make(0.0, pixelConeSpreadAngle);

#if PATH_TRACER_MODE == PATH_TRACER_MODE_BUILD_STABLE_PLANES
        path.SetImageXform(float3x3(1.0, 0.0, 0.0,
                                    0.0, 1.0, 0.0,
                                    0.0, 0.0, 1.0));
        path.setFlag(PathFlags::stablePlaneOnDominantBranch, true);
        path.SetMotionVectorSceneLength(0.0);
#else
        path.SetL(float4(0.0, 0.0, 0.0, 0.0));
        path.SetFireflyFilterK_BsdfScatterPdf(1.0, 0.0);
        path.SetPackedMISInfo_ThpRuRuCorrection(NEEBSDFMISInfo::empty().Pack16bit(), 1.0);
#endif

        path.setStablePlaneIndex(0);
#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        path.InitReferencePrimaryDepth(g_Const.ptConsts.camera.FarZ);
#else
        path.stableBranchID = 1u;
#endif

        if (HasFinishedSurfaceBounces(path.getVertexIndex() + 1,
                                      path.getCounter(PackedCounters::DiffuseBounces)))
            path.setTerminateAtNextBounce();

        return path;
    }

    inline void StartPixel(const PathState path, const WorkingContext workingContext)
    {
#if PATH_TRACER_MODE != PATH_TRACER_MODE_REFERENCE || defined(__INTELLISENSE__)
        workingContext.StablePlanes.StartPixel(path.GetPixelPos());
#endif

#if PATH_TRACER_MODE == PATH_TRACER_MODE_BUILD_STABLE_PLANES
        Bridge::ExportSurfaceInit(path.GetPixelPos());
#endif
    }

    inline void SetupPathPrimaryRay(inout PathState path, const Ray ray)
    {
        path.SetOrigin(ray.origin);
        path.SetDir(ray.dir);
    }

    inline void UpdatePathThroughput(inout PathState path, const float3 weight)
    {
        path.SetThp(path.GetThp() * weight);
    }

    inline bool ShouldCollectGISecondaryRadiance(const PathState path)
    {
        return false;
    }

    inline void UpdateSurfaceOutsideIoR(inout SurfaceData surfaceData, const float outsideIoR)
    {
        const float safeOutsideIoR  = max(outsideIoR, 1.0);
        const float safeInteriorIoR = max(surfaceData.interiorIoR, 1.0);
        surfaceData.bsdf.standardData.SetEta(surfaceData.shadingData.frontFacing ?
                                                 safeOutsideIoR / safeInteriorIoR :
                                                 safeInteriorIoR / safeOutsideIoR);
    }

    inline bool HandleNestedDielectrics(inout SurfaceData surfaceData,
                                        inout PathState path,
                                        const WorkingContext workingContext)
    {
#if RTXPT_NESTED_DIELECTRICS_QUALITY > 0 || defined(__INTELLISENSE__)
        const bool thinSurface = surfaceData.shadingData.mtl.isThinSurface();
        const uint nestedPriority = surfaceData.shadingData.mtl.getNestedPriority();
        const float outsideIoR = ComputeOutsideIoR(path.interiorList,
                                                   surfaceData.shadingData.materialID,
                                                   surfaceData.shadingData.frontFacing);

        if (!thinSurface)
        {
            const bool trueIntersection = path.interiorList.isTrueIntersection(nestedPriority);

            if (!trueIntersection)
            {
                if (path.getCounter(PackedCounters::RejectedHits) < kMaxRejectedDielectricHits)
                {
                    path.incrementCounter(PackedCounters::RejectedHits);
                    path.interiorList.handleIntersection(surfaceData.shadingData.materialID,
                                                         nestedPriority,
                                                         surfaceData.shadingData.frontFacing);
                    path.SetOrigin(ComputeRayOrigin(surfaceData.shadingData.posW,
                                                    -surfaceData.shadingData.faceNCorrected));
                    path.decrementVertexIndex();
                    return false;
                }
#if !NESTED_DIELECTRICS_AVOID_TERMINATION
                else
                {
                    path.terminate();
                    return false;
                }
#endif
            }
        }

        UpdateSurfaceOutsideIoR(surfaceData, outsideIoR);
#endif
        return true;
    }

#if PATH_TRACER_MODE != PATH_TRACER_MODE_BUILD_STABLE_PLANES || defined(__INTELLISENSE__)
    inline BSDFSample MakeBSDFSample(uint lobe, float pdf, float lobeP, float3 weight, float3 wi)
    {
        BSDFSample bs;
        bs.lobe           = lobe;
        // Match upstream IBSDF::getDeltaLobeIndex(): a NON-delta lobe must map to the invalid sentinel
        // (0xFFFFFFFF), not 0. StablePlanesOnScatter feeds this into StablePlanesAdvanceBranchID on every
        // FILL scatter; for a non-delta bounce the advanced branchID must become cStablePlaneInvalidBranchID
        // so the path cleanly leaves the stable-plane branch. The previous value (0) produced a valid-looking
        // branchID on non-delta scatters, corrupting FILL branch tracking so transmission radiance was
        // committed onto a mis-tracked plane and dropped (black glass).
        bs.deltaLobeIndex = ((lobe & kBSDFLobeDelta) == 0u) ? 0xFFFFFFFFu :
            (((lobe & kBSDFLobeTransmission) == 0u) ? 1u : 0u);
        bs.pdf            = pdf;
        bs.lobeP          = lobeP;
        bs.weight         = weight;
        bs.wi             = wi;
        return bs;
    }

    inline void UpdateNestedDielectricsOnScatterTransmission(const StablePlaneShadingData shadingData,
                                                             inout PathState path,
                                                             const WorkingContext workingContext)
    {
#if RTXPT_NESTED_DIELECTRICS_QUALITY > 0 || defined(__INTELLISENSE__)
        if (!shadingData.mtl.isThinSurface())
        {
            const uint nestedPriority = shadingData.mtl.getNestedPriority();
            path.interiorList.handleIntersection(shadingData.materialID, nestedPriority, shadingData.frontFacing);
            path.setInsideDielectricVolume(!path.interiorList.isEmpty());
        }
#endif
    }

    inline NEEResult HandleNEE(const PathState preScatterPath,
                               const SurfaceData surfaceData,
                               inout SampleGenerator sgDirect,
                               inout SampleGenerator sgEnv,
                               const WorkingContext workingContext)
    {
        NEEResult result = NEEResult::empty();

        const bool enableNEE     = workingContext.PtConsts.NEEEnabled != 0u;
        const uint fullSamples   = min(32u, workingContext.PtConsts.NEEFullSamples);
        if (!enableNEE || fullSamples == 0u)
            return result;
        if (!ReferenceShouldRunNEE(preScatterPath, workingContext))
            return result;

        result.BSDFMISInfo.LightSamplingEnabled = true;
        result.BSDFMISInfo.LightSamplingIsSSC   = false;
        result.BSDFMISInfo.CandidateSamples     = min(NEEBSDFMISInfo::SampleCountLimit(), max(1u, workingContext.PtConsts.NEECandidateSamples));
        result.BSDFMISInfo.FullSamples          = min(NEEBSDFMISInfo::SampleCountLimit(), fullSamples);

        const float3 wo = surfaceData.shadingData.V;
        bool sampledEmissive = false;
        float3 directRadiance = SampleDirectLightNEE(surfaceData.bsdf.standardData,
                                                     surfaceData.shadingData.posW,
                                                     surfaceData.shadingData.faceNCorrected,
                                                     surfaceData.shadingData.vertexN,
                                                     surfaceData.shadingData.shadowNoLFadeout,
                                                     wo,
                                                     preScatterPath.GetPixelPos(),
                                                     sgDirect,
                                                     preScatterPath.GetFireflyFilterK(),
                                                     sampledEmissive);
        directRadiance *= preScatterPath.GetThp();

        const bool enableEnvNEE = (workingContext.PtConsts.environmentNEEEnabled & 1u) != 0u;
        if (enableEnvNEE)
        {
            float3 envRadiance = SampleEnvironmentNEE(surfaceData.bsdf.standardData,
                                                      surfaceData.shadingData.posW,
                                                      surfaceData.shadingData.faceNCorrected,
                                                      surfaceData.shadingData.vertexN,
                                                      surfaceData.shadingData.shadowNoLFadeout,
                                                      wo,
                                                      sgEnv,
                                                      preScatterPath.GetFireflyFilterK());
            directRadiance += preScatterPath.GetThp() * envRadiance;
        }

        if (any(directRadiance > 0.0))
        {
            const float specAvg = preScatterPath.hasFlag(PathFlags::stablePlaneBaseScatterDiff) ? 0.0 : Average(directRadiance);
            result.AccumulateRadiance(directRadiance, specAvg);
        }

        return result;
    }

    inline bool GenerateScatterRay(const SurfaceData surfaceData,
                                   inout PathState path,
                                   const WorkingContext workingContext,
                                   out BSDFSample bs)
    {
        bs = MakeBSDFSample(0u, 0.0, 0.0, 0.xxx, 0.xxx);

        const float3 wo = surfaceData.shadingData.V;
        const SampleGeneratorVertexBase sgBase = SampleGeneratorVertexBase::make(path.GetPixelPos(), path.getVertexIndex(), Bridge::getSampleIndex());
        const uint diffuseBounces = path.getCounter(PackedCounters::DiffuseBounces);
        float3 preGeneratedSamples;

#if RTXPT_ENABLE_LOW_DISCREPANCY_SAMPLER_FOR_BSDF
        [branch]
        if (diffuseBounces < workingContext.PtConsts.diffuseBounceCount)
            preGeneratedSamples = SampleSequenceGenerator::Generate(3u, sgBase, kSampleEffect_ScatterBSDF).xyz;
        else
#endif
            preGeneratedSamples = UniformSampleSequenceGenerator::Generate(3u, sgBase, kSampleEffect_ScatterBSDF).xyz;

        float3 wi;
        float3 weight;
        float  pdf;
        uint   lobe;
        float  lobeP;
        if (!SampleBSDF(surfaceData.bsdf.standardData, wo, preGeneratedSamples, wi, weight, pdf, lobe, lobeP))
            return false;

        const bool isTransmission = (lobe & kBSDFLobeTransmission) != 0u;
        const float3 scatterOrigin = ComputeRayOrigin(surfaceData.shadingData.posW,
                                                      isTransmission ? -surfaceData.shadingData.faceNCorrected : surfaceData.shadingData.faceNCorrected);

        path.clearScatterEventFlags();
        path.SetOrigin(scatterOrigin);
        path.SetDir(wi);
        path.SetThp(path.GetThp() * weight);
        path.SetFireflyFilterK_BsdfScatterPdf(ComputeNewScatterFireflyFilterK(path.GetFireflyFilterK(), pdf, lobeP), pdf);
        path.setScatterTransmission(isTransmission);
        path.setScatterSpecular((lobe & (kBSDFLobeSpecularReflection | kBSDFLobeSpecularTransmission | kBSDFLobeDeltaReflection | kBSDFLobeDeltaTransmission)) != 0u);
        path.setScatterDelta((lobe & kBSDFLobeDelta) != 0u);
        path.setDeltaOnlyPath(path.isDeltaOnlyPath() && ((lobe & kBSDFLobeDelta) != 0u));

        if (isTransmission)
            UpdateNestedDielectricsOnScatterTransmission(surfaceData.shadingData, path, workingContext);

#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        const bool isDiffuseBounce =
            ((lobe & kBSDFLobeDiffuseReflection) != 0u) ||
            (((lobe & kBSDFLobeTransmission) == 0u) &&
             surfaceData.bsdf.standardData.roughness > kSpecularRoughnessThreshold);
        if (isDiffuseBounce)
            path.incrementCounter(PackedCounters::DiffuseBounces);
#else
        const bool isDiffuseBounce =
            ((lobe & (kBSDFLobeDiffuseReflection | kBSDFLobeDiffuseTransmission)) != 0u) ||
            surfaceData.bsdf.standardData.roughness > kSpecularRoughnessThreshold;
        if (isDiffuseBounce &&
            !(((lobe & kBSDFLobeDiffuseTransmission) != 0u) && ((path.getVertexIndex() % 2u) == 1u)))
            path.incrementCounter(PackedCounters::DiffuseBounces);
#endif

        bs = MakeBSDFSample(lobe, pdf, lobeP, weight, wi);

#if PATH_TRACER_MODE == PATH_TRACER_MODE_FILL_STABLE_PLANES || defined(__INTELLISENSE__)
        const bool onDominantDenoisingLayer = path.hasFlag(PathFlags::stablePlaneOnDominantBranch);
        const bool isDiffuseForSpecHitT =
            ((lobe & (kBSDFLobeDiffuseReflection | kBSDFLobeDiffuseTransmission)) != 0u) ||
            surfaceData.bsdf.standardData.roughness > 0.35;
        if (onDominantDenoisingLayer && !isDiffuseForSpecHitT)
        {
            if (!surfaceData.shadingData.mtl.isPSDBlockMotionVectorsAtSurface())
            {
                path.setFlag(PathFlags::exportSpecHitTQueued, true);
                Bridge::ExportSpecHitTStart(path);
            }
        }
        else if (path.hasFlag(PathFlags::exportSpecHitTQueued))
        {
            const bool hasNonDeltaLobes =
                (surfaceData.bsdf.standardData.diffuseTransmission > 0.0) ||
                any(surfaceData.bsdf.standardData.diffuse > 0.0) ||
                surfaceData.bsdf.standardData.roughness > 0.0;
            const int maxHitTSpecBounces = 4;
            if (hasNonDeltaLobes || path.getCounter(PackedCounters::BouncesFromStablePlane) > maxHitTSpecBounces)
            {
                Bridge::ExportSpecHitTStop(path);
                path.setFlag(PathFlags::exportSpecHitTQueued, false);
            }
        }

        StablePlanesOnScatter(path, bs, workingContext);
#endif
        return true;
    }
#endif

    inline void AccumulatePathRadiance(const WorkingContext workingContext,
                                       inout PathState path,
                                       float3 radiance,
                                       const float specularRadianceAvg,
                                       bool stablePlaneOnBranch,
                                       bool collectGISecondaryRadiance)
    {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_BUILD_STABLE_PLANES
        workingContext.StablePlanes.AccumulateStableRadiance(path.GetPixelPos(), radiance);
#elif PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        float4 newL = path.GetL();
        newL.rgb += radiance;
        path.SetL(newL);
#elif PATH_TRACER_MODE == PATH_TRACER_MODE_FILL_STABLE_PLANES
        if (!stablePlaneOnBranch)
        {
            float4 newL = float4(radiance, specularRadianceAvg) * Bridge::getNoisyRadianceAttenuation();
            path.SetL(path.GetL() + newL);
        }
#else
#    error Unsupported PATH_TRACER_MODE.
#endif
    }

    inline void AccumulateNEERadiance(const WorkingContext workingContext,
                                      inout PathState path,
                                      const PathState preScatterPath,
                                      const NEEResult neeResult)
    {
        float4 neeRadianceAndSpecAvg = neeResult.GetRadianceAndSpecAvg();
        if (any(neeRadianceAndSpecAvg > 0.0))
        {
            const int bouncesFromStablePlane = preScatterPath.getCounter(PackedCounters::BouncesFromStablePlane) + 1;
            float specRadianceAvg = 0.0;
            if (!preScatterPath.hasFlag(PathFlags::stablePlaneBaseScatterDiff))
            {
                const bool pathIsDeltaOnlyPath = preScatterPath.isDeltaOnlyPath();
                const bool specialCondition = (bouncesFromStablePlane == 1) || (pathIsDeltaOnlyPath && bouncesFromStablePlane <= 3);
                specRadianceAvg = specialCondition ? neeRadianceAndSpecAvg.w : Average(neeRadianceAndSpecAvg.rgb);
            }

            AccumulatePathRadiance(workingContext,
                                   path,
                                   neeRadianceAndSpecAvg.rgb,
                                   specRadianceAvg,
                                   false,
                                   ShouldCollectGISecondaryRadiance(preScatterPath));
        }
    }

    inline void CommitPixel(const PathState path, const WorkingContext workingContext)
    {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_BUILD_STABLE_PLANES
        // Stable radiance and stable-plane data are committed incrementally while tracing.
#elif PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        const uint2 pixelPos = path.GetPixelPos();
        workingContext.OutputColor[pixelPos]   = float4(path.GetL().rgb, 1.0);
        workingContext.Depth[pixelPos]         = path.GetReferencePrimaryDepth();
        workingContext.MotionVectors[pixelPos] = float4(0.0, 0.0, 0.0, 0.0);
#elif PATH_TRACER_MODE == PATH_TRACER_MODE_FILL_STABLE_PLANES
        workingContext.StablePlanes.CommitDenoiserRadiance(path);
#else
#    error Unsupported PATH_TRACER_MODE.
#endif
    }

    inline bool HandleRussianRoulette(inout PathState path,
                                      const PathState preScatterPath,
                                      const WorkingContext workingContext)
    {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        if (preScatterPath.getVertexIndex() <= workingContext.PtConsts.minBounceCount)
            return true;

        SampleGenerator sgRR = SampleGenerator_makeStateless(preScatterPath.GetPixelPos(),
                                                             preScatterPath.getVertexIndex(),
                                                             Bridge::getSampleIndex(),
                                                             kSampleEffect_RussianRoulette);
        const float3 thp     = path.GetThp();
        const float  survive = clamp(max(thp.x, max(thp.y, thp.z)), 0.05, 1.0);
        if (sampleNext1D(sgRR) > survive)
        {
            path.terminate();
            return false;
        }

        path.SetThp(thp / survive);
#endif
        return true;
    }

#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
    inline void UpdatePathTravelledLengthOnly(inout PathState path, const float rayTCurrent)
    {
        const float rayConeSpreadAngle = path.rayCone.getSpreadAngle();
        path.rayCone = RayCone::make(path.rayCone.getWidth() + rayConeSpreadAngle * rayTCurrent,
                                     rayConeSpreadAngle);
        path.SetSceneLength(min(path.GetSceneLength() + rayTCurrent, kMaxRayTravel));
    }
#endif

    inline void UpdatePathTravelled(inout PathState path,
                                    const float3 rayOrigin,
                                    const float3 rayDir,
                                    const float rayTCurrent,
                                    const WorkingContext workingContext)
    {
        path.incrementVertexIndex();
        UpdatePathTravelledLengthOnly(path, rayTCurrent);
    }

    inline void HandleMiss(inout PathState path,
                           const float3 rayOrigin,
                           const float3 rayDir,
                           const float rayTCurrent,
                           const WorkingContext workingContext)
    {
        UpdatePathTravelled(path, rayOrigin, rayDir, rayTCurrent, workingContext);

#if PATH_TRACER_MODE == PATH_TRACER_MODE_FILL_STABLE_PLANES
        if (path.hasFlag(PathFlags::exportSpecHitTQueued))
        {
            Bridge::ExportSpecHitTStop(path);
            path.setFlag(PathFlags::exportSpecHitTQueued, false);
        }
#endif

        EnvMapSampler envSampler = RTXPTCreateEnvMapSampler(Bridge::getEnvMapConstants());
        float3 environmentEmission = envSampler.Eval(rayDir, 0.0);

#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        const NEEBSDFMISInfo prevMISInfo = NEEBSDFMISInfo::Unpack16bit(path.GetPackedMISInfo());
        const bool didEnvNEE =
            prevMISInfo.LightSamplingEnabled &&
            ((workingContext.PtConsts.environmentNEEEnabled & 1u) != 0u);
        environmentEmission *= ComputeBSDFEnvMISWeight(didEnvNEE, path.GetBsdfScatterPdf(), rayDir);
        environmentEmission = ApplyReferenceFireflyFilter(environmentEmission, path, workingContext);
#endif

#if PATH_TRACER_MODE != PATH_TRACER_MODE_REFERENCE || defined(__INTELLISENSE__)
        StablePlanesHandleMiss(path, environmentEmission, rayOrigin, rayDir, rayTCurrent, workingContext);
#endif

        if (any(environmentEmission > 0.0))
        {
            const float3 radiance = path.GetThp() * environmentEmission;
            const float specRadianceAvg =
                path.hasFlag(PathFlags::stablePlaneBaseScatterDiff) ? 0.0 : Average(radiance);
            AccumulatePathRadiance(workingContext,
                                   path,
                                   radiance,
                                   specRadianceAvg,
                                   path.hasFlag(PathFlags::stablePlaneOnBranch),
                                   ShouldCollectGISecondaryRadiance(path));
        }

        path.clearHit();
        path.terminate();
    }

    inline void HandleHit(inout PathState path,
                          SurfaceData surfaceData,
                          const float3 surfaceEmission,
                          const float3 rayOrigin,
                          const float3 rayDir,
                          const float rayTCurrent,
                          const WorkingContext workingContext)
    {
        UpdatePathTravelled(path, rayOrigin, rayDir, rayTCurrent, workingContext);

#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        path.CaptureReferencePrimaryDepth(rayTCurrent);
#endif

        // [Packing refactor — Gate 2] Nested-dielectric handling restored to realtime HandleHit on top
        // of the split flags/vertexIndex packing (Gate 1). Previously the mere presence of this block
        // blacked out opaque primary hits via a DXC miscompilation of the shared flagsAndVertexIndex
        // word, even though opaque hits execute none of it (thin surface / empty interior list). If
        // opaque shading stays correct now, the de-aliasing resolved the miscompilation and realtime
        // transmission through nested dielectrics is back.
        float volumeAbsorption = 0.0;
#if RTXPT_NESTED_DIELECTRICS_QUALITY > 0 || defined(__INTELLISENSE__)
        if (!path.interiorList.isEmpty())
        {
            const HomogeneousVolumeData volume = Bridge::loadHomogeneousVolumeData(path.interiorList.getTopMaterialID());
            const float3 transmittance = HomogeneousVolumeSampler::evalTransmittance(volume, rayTCurrent);
            volumeAbsorption = 1.0 - luminance(transmittance);
            UpdatePathThroughput(path, transmittance);
        }
#endif

        const bool rejectedFalseHit = !HandleNestedDielectrics(surfaceData, path, workingContext);
        if (rejectedFalseHit)
        {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_BUILD_STABLE_PLANES
            if (path.isTerminated())
                StablePlanesHandleMiss(path, 0.0.xxx, rayOrigin, rayDir, rayTCurrent, workingContext);
#endif
            return;
        }

        float3 referenceSurfaceEmission = surfaceEmission;
#if PATH_TRACER_MODE == PATH_TRACER_MODE_REFERENCE
        const NEEBSDFMISInfo prevMISInfo = NEEBSDFMISInfo::Unpack16bit(path.GetPackedMISInfo());
        const bool didEmissiveNEE =
            prevMISInfo.LightSamplingEnabled &&
            prevMISInfo.FullSamples > 0u &&
            Bridge::getEmissiveTriangleCount() > 0u;
        if (didEmissiveNEE && path.GetBsdfScatterPdf() > 0.0 && surfaceData.emissiveLightPdf > 0.0)
        {
            referenceSurfaceEmission *= ComputeBSDFMISForEmissiveTriangle(path.GetPixelPos(),
                                                                          path.GetBsdfScatterPdf(),
                                                                          surfaceData.emissiveLightPdf,
                                                                          prevMISInfo.FullSamples);
        }
        referenceSurfaceEmission = ApplyReferenceFireflyFilter(referenceSurfaceEmission, path, workingContext);
#endif

        if (any(referenceSurfaceEmission > 0.0))
        {
            const float3 radiance = path.GetThp() * referenceSurfaceEmission;
            const float specRadianceAvg =
                path.hasFlag(PathFlags::stablePlaneBaseScatterDiff) ? 0.0 : Average(radiance);
            AccumulatePathRadiance(workingContext,
                                   path,
                                   radiance,
                                   specRadianceAvg,
                                   path.hasFlag(PathFlags::stablePlaneOnBranch),
                                   ShouldCollectGISecondaryRadiance(path));
        }
        const bool pathStopping = path.isTerminatingAtNextBounce();
#if PATH_TRACER_MODE == PATH_TRACER_MODE_BUILD_STABLE_PLANES
        StablePlanesHandleHit(path,
                              rayOrigin,
                              rayDir,
                              rayTCurrent,
                              workingContext,
                              surfaceData,
                              volumeAbsorption,
                              surfaceEmission,
                              pathStopping);
#endif

        if (pathStopping || !path.isActive())
        {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_FILL_STABLE_PLANES
            if (pathStopping && path.isActive())
            {
                const PathState preScatterPath = path;
                SampleGenerator sgDirect = SampleGenerator_makeStateless(preScatterPath.GetPixelPos(),
                                                                         preScatterPath.getVertexIndex(),
                                                                         Bridge::getSampleIndex(),
                                                                         kSampleEffect_NEELightSampler);
                SampleGenerator sgEnv = SampleGenerator_makeStateless(preScatterPath.GetPixelPos(),
                                                                      preScatterPath.getVertexIndex(),
                                                                      Bridge::getSampleIndex(),
                                                                      kSampleEffect_NextEventEstimation);
                const NEEResult neeResult = HandleNEE(preScatterPath, surfaceData, sgDirect, sgEnv, workingContext);
                AccumulateNEERadiance(workingContext, path, preScatterPath, neeResult);
            }
#endif
#if PATH_TRACER_MODE == PATH_TRACER_MODE_FILL_STABLE_PLANES
            if (path.hasFlag(PathFlags::exportSpecHitTQueued))
            {
                Bridge::ExportSpecHitTStop(path);
                path.setFlag(PathFlags::exportSpecHitTQueued, false);
            }
#endif
            path.terminate();
            return;
        }

#if PATH_TRACER_MODE == PATH_TRACER_MODE_BUILD_STABLE_PLANES
        return;
#else
        const PathState preScatterPath = path;
        BSDFSample bs;
        const bool scatterValid = GenerateScatterRay(surfaceData, path, workingContext, bs);

        SampleGenerator sgDirect = SampleGenerator_makeStateless(preScatterPath.GetPixelPos(),
                                                                 preScatterPath.getVertexIndex(),
                                                                 Bridge::getSampleIndex(),
                                                                 kSampleEffect_NEELightSampler);
        SampleGenerator sgEnv = SampleGenerator_makeStateless(preScatterPath.GetPixelPos(),
                                                              preScatterPath.getVertexIndex(),
                                                              Bridge::getSampleIndex(),
                                                              kSampleEffect_NextEventEstimation);
        NEEResult neeResult = HandleNEE(preScatterPath, surfaceData, sgDirect, sgEnv, workingContext);
        path.SetPackedMISInfo_ThpRuRuCorrection(neeResult.BSDFMISInfo.Pack16bit(), path.GetThpRuRuCorrection());
        AccumulateNEERadiance(workingContext, path, preScatterPath, neeResult);

        if (!scatterValid)
        {
#if PATH_TRACER_MODE == PATH_TRACER_MODE_FILL_STABLE_PLANES
            if (path.hasFlag(PathFlags::exportSpecHitTQueued))
            {
                Bridge::ExportSpecHitTStop(path);
                path.setFlag(PathFlags::exportSpecHitTQueued, false);
            }
#endif
            path.terminate();
            return;
        }

        const bool shouldTerminate = HasFinishedSurfaceBounces(path.getVertexIndex() + 1,
                                                               path.getCounter(PackedCounters::DiffuseBounces));
        if (shouldTerminate)
        {
            path.setTerminateAtNextBounce();
        }
        else if (!HandleRussianRoulette(path, preScatterPath, workingContext))
        {
            return;
        }
#endif
    }
} // namespace PathTracer

#endif // __PATH_TRACER_HLSLI__
